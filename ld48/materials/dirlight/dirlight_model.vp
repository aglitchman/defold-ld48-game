attribute highp vec4 position;
attribute mediump vec2 texcoord0;
attribute mediump vec3 normal;

uniform highp mat4 mtx_worldview;
uniform highp mat4 mtx_world;
uniform highp mat4 mtx_view;
uniform highp mat4 mtx_proj;
// uniform highp mat4 mtx_normal;
// uniform highp vec4 light;

varying highp vec4 var_position;
varying mediump vec3 var_normal;
varying mediump vec2 var_texcoord0;
// varying mediump vec4 var_light;

//
// WebGL 1.0, OpenGL ES 2.0, OpenGL 3.1 don't have "transpose" and "inverse" functions.
//
mat3 mat_transpose(mat3 m) {
    return mat3(m[0][0], m[1][0], m[2][0],
        m[0][1], m[1][1], m[2][1],
        m[0][2], m[1][2], m[2][2]);
}

mat3 mat_inverse(mat3 m) {
    float a00 = m[0][0], a01 = m[0][1], a02 = m[0][2];
    float a10 = m[1][0], a11 = m[1][1], a12 = m[1][2];
    float a20 = m[2][0], a21 = m[2][1], a22 = m[2][2];

    float b01 = a22 * a11 - a12 * a21;
    float b11 = -a22 * a10 + a12 * a20;
    float b21 = a21 * a10 - a11 * a20;

    float det = a00 * b01 + a01 * b11 + a02 * b21;

    return mat3(b01, (-a22 * a01 + a02 * a21), (a12 * a01 - a02 * a11),
    b11, (a22 * a00 - a02 * a20), (-a12 * a00 + a02 * a10),
    b21, (-a21 * a00 + a01 * a20), (a11 * a00 - a01 * a10)) / det;
}
//
//
//

void main()
{
    vec4 p = mtx_worldview * vec4(position.xyz, 1.0);
    // var_light = mtx_view * vec4(light.xyz, 1.0);
    var_position = p;
    var_texcoord0 = texcoord0;
    // "world" normal:
    var_normal = normalize(mat_transpose(mat_inverse(mat3(mtx_world))) * normal);
    gl_Position = mtx_proj * p;
}

